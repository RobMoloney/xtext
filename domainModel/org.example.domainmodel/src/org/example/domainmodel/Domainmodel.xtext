grammar org.example.domainmodel.Domainmodel with
                                      org.eclipse.xtext.xbase.Xbase
 
generate domainmodel "http://www.example.org/domainmodel/Domainmodel"
 
import "http://www.eclipse.org/xtext/common/JavaVMTypes"
 
Domainmodel:
    importSection=XImportSection?
    elements+=AbstractElement*;
 
AbstractElement:
    PackageDeclaration | Entity;
 
PackageDeclaration:
    'storyBoy' name=QualifiedName '{'
        elements+=AbstractElement*
    '}';
 
Entity:
    'entity' name=ValidID ('extends' superType=JvmTypeReference)? '{'
        features+=Feature*
    '}';
 
Feature:
    Property | Operation;
 
Property:
    name=ValidID ':' type=JvmTypeReference;
 
Operation:
    'op' name=ValidID 
        '('(params+=FullJvmFormalParameter 
        (',' params+=FullJvmFormalParameter)*)?')'
        ':' type=JvmTypeReference 
    body=XBlockExpression;

@Override 
XImportDeclaration:
	'herla' (
		(static?='static' extension?='extension'? importedType=[JvmDeclaredType|QualifiedNameInStaticImport] (wildcard?='*' | memberName=ValidID))
		| importedType=[JvmDeclaredType|QualifiedName]
		| importedNamespace=QualifiedNameWithWildcard) 'like'
;